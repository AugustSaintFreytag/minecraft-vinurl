plugins {
	id "fabric-loom" version "${plg_fabric_loom}"
	id "org.moddedmc.wiki.toolkit" version "${plg_moddedmc_wiki}"
}

version = "${mod_version}+mc${mod_minecraft_version_end}"

base {
	archivesName = mod_id
}

repositories {
	maven { url = "https://maven.shedaniel.me" }
	maven { url = "https://maven.terraformersmc.com" }
	maven { url = "https://maven.wispforest.io" }
	
}

dependencies {
	// Minecraft
	
	minecraft "com.mojang:minecraft:${run_minecraft_version}"
	mappings "net.fabricmc:yarn:${run_yarn_mappings}:v2"

	// Fabric

	modImplementation "net.fabricmc:fabric-loader:${run_fabric_loader_version}"
	modImplementation "net.fabricmc.fabric-api:fabric-api:${run_fabric_version}"
	
	// Config

	modImplementation("me.shedaniel.cloth:cloth-config-fabric:${project.run_cloth_config_version}") {
		exclude(group: "net.fabricmc.fabric-api")
  	}
	
	// Mod Menu

	modImplementation("com.terraformersmc:modmenu:${project.run_mod_menu_version}") {
		exclude(group: "net.fabricmc.fabric-api")
	}

	// Jorbis

	include implementation("org.jcraft:jorbis:0.0.17")

	// Owo Lib

	annotationProcessor modImplementation("io.wispforest:owo-lib:${run_owo_version}")
}

processResources {
	var replaceProperties = [
			mod_version               		: mod_version,
			mod_id							: mod_id,
			mod_name						: mod_name,
			mod_license                     : mod_license,
			mod_author                      : mod_author,
			mod_credits                     : mod_credits,
			mod_description                 : mod_description,
			mod_url                         : mod_url,

			mod_minecraft_version_start		: mod_minecraft_version_start,
			mod_minecraft_version_end		: mod_minecraft_version_end,

			mod_loader_fabric_version		: mod_loader_fabric_version,
			mod_loader_neoforge_version		: mod_loader_neoforge_version
	]
	inputs.properties replaceProperties
	filesMatching(['fabric.mod.json', 'META-INF/neoforge.mods.toml']) {
		expand replaceProperties
	}
}

tasks.withType(JavaCompile).configureEach {
	it.options.release = 17
}

java {
	sourceCompatibility = JavaVersion.VERSION_17
	targetCompatibility = JavaVersion.VERSION_17
}

jar {
	if (!project.hasProperty('neoforge')) {
		exclude('META-INF/**')
	}
	from("LICENSE") {
		rename { "${it}_${mod_id}" }
	}
}